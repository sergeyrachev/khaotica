video_sequence() {
    next_start_code()
    sequence_header()
    if ( nextbits() == '0000 0000  0000 0000  0000 0001  1011 0101' ) {
        sequence_extension()
        do {
            extension_and_user_data( 0 )
            do {
                if (nextbits() == '0000 0000  0000 0000  0000 0001  1011 1000') {
                    group_of_pictures_header()
                    extension_and_user_data( 1 )
                }
                picture_header()
                picture_coding_extension()
                extension_and_user_data( 2 )
                picture_data()
            } while ( (nextbits() == '0000 0000  0000 0000 0000 0001 0000 0000' ) || (nextbits() == '0000 0000  0000 0000  0000 0001  1011 1000') )
            if ( nextbits() != '0000 0000  0000 0000  0000 0001  1011 0111' ) {
                sequence_header()
                sequence_extension()
            }
        } while ( nextbits() != '0000 0000  0000 0000  0000 0001  1011 0111' )
    } else {
    /* ISO/IEC 11172-2 */
    }
    sequence_end_code 32 bslbf
}

sequence_header() {
    sequence_header_code 32 bslbf
    horizontal_size_value 12 uimsbf
    vertical_size_value 12 uimsbf
    aspect_ratio_information 4 uimsbf
    frame_rate_code 4 uimsbf
    bit_rate_value 18 uimsbf
    marker_bit 1 bslbf
    vbv_buffer_size_value 10 uimsbf
    constrained_parameters_flag 1
    load_intra_quantiser_matrix 1
    if ( load_intra_quantiser_matrix )
        intra_quantiser_matrix[64] 8*64 uimsbf
    load_non_intra_quantiser_matrix 1
    if ( load_non_intra_quantiser_matrix )
        non_intra_quantiser_matrix[64] 8*64 uimsbf
    next_start_code()
}

extension_and_user_data( i ) {
    while ( ( nextbits()== '0000 0000 0000 0000 0000 0001 1011 0101' ) || ( nextbits()== '0000 0000 0000 0000 0000 0001 1011 0010' ) ) {
        if ( i != 1 )
            if ( nextbits()== extension_start_code )
                extension_data( i )
        if ( nextbits()== user_data_start_code )
            user_data()
    }
}

extension_data( i ) {
    while ( nextbits()== extension_start_code ) {
        extension_start_code 32 bslbf
        if (i == 0) { /* follows sequence_extension() */
            if ( nextbits()== '0010' )
                sequence_display_extension()
            else
                sequence_scalable_extension()
        }
        /* NOTE - i never takes the value 1 because extension_data()
            never follows a group_of_pictures_header() */
        if (i == 2) { /* follows picture_coding_extension() */
            if ( nextbits() == '0011' )
                quant_matrix_extension()
            else if ( nextbits() == '0100' )
                copyright_extension()
            else if ( nextbits() == '0111' )
                picture_display_extension()
            else if ( nextbits() == '1001' )
                picture_spatial_scalable_extension()
            else
                picture_temporal_scalable_extension()
        }
    }
}

user_data() {
    user_data_start_code 32 bslbf
    while( nextbits() != '0000 0000 0000 0000 0000 0001' ) {
        user_data 8 uimsbf
    }
    next_start_code()
}

sequence_extension() {
    extension_start_code 32 bslbf
    extension_start_code_identifier 4 uimsbf
    profile_and_level_indication 8 uimsbf
    progressive_sequence 1 uimsbf
    chroma_format 2 uimsbf
    horizontal_size_extension 2 uimsbf
    vertical_size_extension 2 uimsbf
    bit_rate_extension 12 uimsbf
    marker_bit 1 bslbf
    vbv_buffer_size_extension 8 uimsbf
    low_delay 1 uimsbf
    frame_rate_extension_n 2 uimsbf
    frame_rate_extension_d 5 uimsbf
    next_start_code()
}

sequence_display_extension() {
    extension_start_code_identifier 4 uimsbf
    video_format 3 uimsbf
    colour_description 1 uimsbf
    if ( colour_description ) {
        colour_primaries 8 uimsbf
        transfer_characteristics 8 uimsbf
        matrix_coefficients 8 uimsbf
    }
    display_horizontal_size 14 uimsbf
    marker_bit 1 bslbf
    display_vertical_size 14 uimsbf
    next_start_code()
}

sequence_scalable_extension() {
    extension_start_code_identifier 4 uimsbf
    scalable_mode 2 uimsbf
    layer_id 4 uimsbf
    if (scalable_mode == 1) {
        lower_layer_prediction_horizontal_size 14 uimsbf
        marker_bit 1 bslbf
        lower_layer_prediction_vertical_size 14 uimsbf
        horizontal_subsampling_factor_m 5 uimsbf
        horizontal_subsampling_factor_n 5 uimsbf
        vertical_subsampling_factor_m 5 uimsbf
        vertical_subsampling_factor_n 5 uimsbf
    }
    if ( scalable_mode == 3 ) {
        picture_mux_enable 1 uimsbf
        if ( picture_mux_enable )
            mux_to_progressive_sequence 1 uimsbf
        picture_mux_order 3 uimsbf
        picture_mux_factor 3 uimsbf
    }
    next_start_code()
}

group_of_pictures_header() {
    group_start_code 32 bslbf
    drop_frame_flag 1 uimsbf
    time_code_hours 5 uimsbf
    time_code_minutes 6 uimsbf
    marker_bit 1 bslbf
    time_code_seconds 6 uimsbf
    time_code_pictures 6 uimsbf
    closed_gop 1 uimsbf
    broken_link 1 uimsbf
    next_start_code()
}

picture_header() {
    picture_start_code 32 bslbf
    temporal_reference 10 uimsbf
    picture_coding_type 3 uimsbf
    vbv_delay 16 uimsbf
    if ( picture_coding_type == 2 || picture_coding_type == 3) {
        full_pel_forward_vector 1
        forward_f_code 3 uimsbf
    }
    if ( picture_coding_type == 3 ) {
        full_pel_backward_vector 1
        backward_f_code 3 uimsbf
    }
    while ( nextbits() == '1' ) {
        extra_bit_picture /* with the value "1" */ 1 uimsbf
        extra_information_picture 8
    }
    extra_bit_picture /* with the value "0" */ 1 uimsbf
    next_start_code()
}

picture_coding_extension() {
    extension_start_code 32 bslbf
    extension_start_code_identifier 4 uimsbf
    f_code[0][0] /* forward horizontal */ 4 uimsbf
    f_code[0][1] /* forward vertical */ 4 uimsbf
    f_code[1][0] /* backward horizontal */ 4 uimsbf
    f_code[1][1] /* backward vertical */ 4 uimsbf
    intra_dc_precision 2 uimsbf
    picture_structure 2 uimsbf
    top_field_first 1 uimsbf
    frame_pred_frame_dct 1 uimsbf
    concealment_motion_vectors 1 uimsbf
    q_scale_type 1 uimsbf
    intra_vlc_format 1 uimsbf
    alternate_scan 1 uimsbf
    repeat_first_field 1 uimsbf
    chroma_420_type 1 uimsbf
    progressive_frame 1 uimsbf
    composite_display_flag 1 uimsbf
    if ( composite_display_flag ) {
        v_axis 1 uimsbf
        field_sequence 3 uimsbf
        sub_carrier 1 uimsbf
        burst_amplitude 7 uimsbf
        sub_carrier_phase 8 uimsbf
    }
    next_start_code()
}

quant_matrix_extension() {
    extension_start_code_identifier 4 uimsbf
    load_intra_quantiser_matrix 1 uimsbf
    if ( load_intra_quantiser_matrix )
        intra_quantiser_matrix[64] 8 * 64 uimsbf
    load_non_intra_quantiser_matrix 1 uimsbf
    if ( load_non_intra_quantiser_matrix )
        non_intra_quantiser_matrix[64] 8 * 64 uimsbf
    load_chroma_intra_quantiser_matrix 1 uimsbf
    if ( load_chroma_intra_quantiser_matrix )
        chroma_intra_quantiser_matrix[64] 8 * 64 uimsbf
    load_chroma_non_intra_quantiser_matrix 1 uimsbf
    if ( load_chroma_non_intra_quantiser_matrix )
        chroma_non_intra_quantiser_matrix[64] 8 * 64 uimsbf
    next_start_code()
}

picture_display_extension() {
    extension_start_code_identifier 4 uimsbf
    for ( i=0; i<number_of_frame_centre_offsets; i++ ) {
        frame_centre_horizontal_offset 16 simsbf
        marker_bit 1 bslbf
        frame_centre_vertical_offset 16 simsbf
        marker_bit 1 bslbf
    }
    next_start_code()
}

picture_temporal_scalable_extension() {
    extension_start_code_identifier 4 uimsbf
    reference_select_code 2 uimsbf
    forward_temporal_reference 10 uimsbf
    marker_bit 1 bslbf
    backward_temporal_reference 10 uimsbf
    next_start_code()
}

picture_spatial_scalable_extension() {
    extension_start_code_identifier 4 uimsbf
    lower_layer_temporal_reference 10 uimsbf
    marker_bit 1 bslbf
    lower_layer_horizontal_offset 15 simsbf
    marker_bit 1 bslbf
    lower_layer_vertical_offset 15 simsbf
    spatial_temporal_weight_code_table_index 2 uimsbf
    lower_layer_progressive_frame 1 uimsbf
    lower_layer_deinterlaced_field_select 1 uimsbf
    next_start_code()
}

copyright_extension() {
    extension_start_code_identifier 4 uimsbf
    copyright_flag 1 bslbf
    copyright_identifier 8 uimsbf
    original_or_copy 1 bslbf
    reserved 7 uimsbf
    marker_bit 1 bslbf
    copyright_number_1 20 uimsbf
    marker_bit 1 bslbf
    copyright_number_2 22 uimsbf
    marker_bit 1 bslbf
    copyright_number_3 22 uimsbf
    next_start_code()
}

picture_data() {
    do {
        slice()
    } while ( nextbits() >= '0000 0000 0000 0000 0000 0001 0000 0001' && nextbits() <= '0000 0000 0000 0000 0000 0001 1010 1111')
    next_start_code()
}

slice() {
    slice_start_code 32 bslbf
    if (vertical_size_value > 2800)
    slice_vertical_position_extension 3 uimsbf
    if ( __position(sequence_scalable_extension) )
        if (scalable_mode == '00' )
            priority_breakpoint 7 uimsbf
    quantiser_scale_code 5 uimsbf
    if ( nextbits() == '1' ) {
        intra_slice_flag 1 bslbf
        intra_slice 1 uimsbf
        reserved_bits 7 uimsbf
        while ( nextbits() == '1' ) {
            extra_bit_slice /* with the value "1" */ 1 uimsbf
            extra_information_slice 8
        }
    }

    extra_bit_slice /* with the value "0" */ 1 uimsbf
    do {
        __skip(1)
    } while ( nextbits() != '000 0000 0000 0000 0000 0000' )
    next_start_code()
}

next_start_code() {
    while ( !bytealigned() )
        zero_bit 1 "0"
    while ( nextbits() != '0000 0000 0000 0000 0000 0001' )
        zero_byte 8 "0000 0000"
}
